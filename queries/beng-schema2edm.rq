PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc:      <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX void:    <http://rdfs.org/ns/void#>
PREFIX edm:     <http://www.europeana.eu/schemas/edm/>
PREFIX ore:     <http://www.openarchives.org/ore/terms/>
PREFIX sdo:     <https://schema.org/>

CONSTRUCT {
    ?uri_cho a edm:ProvidedCHO ;
         edm:type ?edm_type ;
         dc:creator ?creator ;
         dc:description ?description ;
         dc:type ?genre ;
         dc:subject ?subject ;
         dc:title ?name ;
         dcterms:alternative ?altName ;
         dc:publisher ?publisher ;
         owl:sameAs ?sameAs ;
         dcterms:spatial ?spatial ;
         dcterms:temporal ?temporal ;
         dcterms:medium ?material .   

    ?uri_ore a ore:Aggregation ;
         edm:aggregatedCHO ?uri_cho ;
         edm:provider ?provider ;
         edm:dataProvider ?dataProvider ;
         edm:rights ?license ;
         edm:isShownBy ?isShownBy ;
         edm:isShownAt ?isShownAt .

    ?uri_org a edm:Agent ;
         skos:prefLabel ?org_name ;
         skos:note ?org_description .

    ?uri_pers a edm:Agent ;
         skos:prefLabel ?pers_name ;
         skos:altLabel ?pers_additionalName ;
         edm:begin ?pers_birthDate ;
         edm:end ?pers_deathDate .

    ?uri_pl a edm:Place ;
        skos:prefLabel ?pl_name ;
        skos:altLabel ?pl_additionalName ;
        owl:sameAs ?pl_sameAs .
}
WHERE {
    {
      {
	    ?uri_cho a ?type .
        VALUES ?type { sdo:Sculpture sdo:VisualArtwork sdo:Painting sdo:Photograph }
        BIND(("IMAGE") as ?edm_type)
      }
      UNION 
      {
        ?uri_cho a sdo:Book .
        BIND(("TEXT") as ?edm_type)
      }
      UNION 
      {
        ?uri_cho a sdo:CreativeWork ;
                sdo:associatedMedia [rdf:type sdo:AudioObject] .
        BIND(("SOUND") as ?edm_type)
      }
      UNION 
      {
        ?uri_cho a sdo:CreativeWork ;
                sdo:associatedMedia [rdf:type sdo:VideoObject] .
        BIND(("VIDEO") as ?edm_type)
      }

      # Generate an "URI_CHO#agg" URI pointing to a ore:Aggregation resource. 
      BIND( URI(CONCAT(STR(?uri_cho),"#agg")) as ?uri_ore)

      # NOTE: The VAR_PROVIDER will be replaced during runtime by the starter.sh script.
      # Europeana requires the provider value to be specified through the aggregation process.
      BIND( STR('VAR_PROVIDER') as ?provider)

      OPTIONAL { ?uri_cho sdo:creator ?creator }
      OPTIONAL { ?uri_cho sdo:description ?description }
      OPTIONAL { ?uri_cho sdo:genre ?genre . 
                FILTER (lang( ?genre) = "en")
                BIND(str(?genre) as ?dc_genre)
               }
      OPTIONAL { ?uri_cho sdo:keywords ?subject }
      OPTIONAL { ?uri_cho sdo:about ?subject }
      OPTIONAL { ?uri_cho sdo:license ?cho_license }
      OPTIONAL { ?uri_cho sdo:name ?name }
      OPTIONAL { ?uri_cho sdo:alternateName ?altName }
      OPTIONAL { 
         # use a default the provider mentioned in the dataset description, if any...
         ?dataset void:rootResource ?uri_cho .
         ?dataset sdo:provider ?dataProvider . 
      }
      OPTIONAL { 
         # use as default the license statement mentioned in the dataset description, if any...
         ?dataset void:rootResource ?uri_cho .
         ?dataset sdo:license ?dataset_license .
      }
      OPTIONAL { 
         # use the provider from the resource description
         ?uri_cho sdo:provider ?dataProvider 
      }
      OPTIONAL { ?uri_cho sdo:publisher ?publisher }
      OPTIONAL { ?uri_cho sdo:sameAs ?sameAs }
      OPTIONAL { ?uri_cho sdo:spatial ?spatial }
      OPTIONAL { ?uri_cho sdo:temporal ?temporal }
      OPTIONAL { ?uri_cho sdo:material ?material }
      OPTIONAL {
        # Europeana requires valid URI for edm:isShownAt 
        ?uri_cho sdo:url ?url 
        BIND(IRI(?url) as ?isShownAt)  
      }
      OPTIONAL {
        # Europeana requires valid URI for edm:isShownBy 
        ?uri_cho sdo:image ?image
        BIND(IRI(?image) as ?isShownBy)
      }
      BIND(COALESCE(IRI(?cho_license),IRI(?dataset_license),"no license info") as ?license)
    }
    UNION 
    {
	    ?uri_org a sdo:Organization .
        OPTIONAL { ?uri_org sdo:name ?org_name }
        OPTIONAL { ?uri_org sdo:description ?org_desc }
    }
    UNION
    {
	    ?uri_pers a sdo:Person .
        OPTIONAL { ?uri_pers sdo:name ?pers_name }
        OPTIONAL { ?uri_pers sdo:additionalName ?pers_additionalName }
        OPTIONAL { ?uri_pers sdo:birthDate ?pers_birthDate }
        OPTIONAL { ?uri_pers sdo:deathDate ?pers_deathDate }
        OPTIONAL { ?uri_pers sdo:description ?pers_desc }        
    }
    UNION
    {
	    ?uri_pl a sdo:Place .
        OPTIONAL { ?uri_pl sdo:name ?pl_name }
        OPTIONAL { ?uri_pl sdo:alternateName ?pl_alternateName }
        OPTIONAL { ?uri_pl sdo:sameAs ?pl_sameAs }        
    }
}