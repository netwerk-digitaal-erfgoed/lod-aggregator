PREFIX owl:     <http://www.w3.org/2002/07/owl#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dc:      <http://purl.org/dc/elements/1.1/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX void:    <http://rdfs.org/ns/void#>
PREFIX schema:  <http://schema.org/>
PREFIX edm:     <http://www.europeana.eu/schemas/edm/>
PREFIX ore:     <http://www.openarchives.org/ore/terms/>

CONSTRUCT {
    ?uri_cho a edm:ProvidedCHO ;
         edm:type ?edm_type ;
         dc:creator ?creator ;
         dc:description ?description ;
         dc:type ?genre ;
         dc:subject ?subject ;
         dc:title ?name ;
         dc:publisher ?publisher ;
         owl:sameAs ?sameAs ;
         dcterms:spatial ?spatial ;
         dcterms:temporal ?temporal ;
         dc:language "el" ;
         dcterms:medium ?material .   

    ?uri_ore a ore:Aggregation ;
         edm:aggregatedCHO ?uri_cho ;
         edm:provider ?provider ;
         edm:dataProvider ?provider ;
         dc:rights ?license ;
         edm:isShownBy ?isShownBy ;
         edm:isShownAt ?url .

    ?uri_org a edm:Agent ;
         skos:prefLabel ?org_name ;
         skos:note ?org_description .

    ?uri_pers a edm:Agent ;
         skos:prefLabel ?pers_name ;
         skos:altLabel ?pers_additionalName ;
         edm:begin ?pers_birthDate ;
         edm:end ?pers_deathDate .

    ?uri_pl a edm:Place ;
        skos:prefLabel ?pl_name ;
        skos:altLabel ?pl_additionalName ;
        owl:sameAs ?pl_sameAs .
}
WHERE {
    {
      {
	   ?uri_cho a schema:Sculpture .
        BIND(("IMAGE") as ?edm_type)
      }
      UNION 
      {
	   ?uri_cho a schema:VisualArtwork .
        BIND(("IMAGE") as ?edm_type)
      }
      UNION
      {
        ?uri_cho a schema:Book .
        BIND(("TEXT") as ?edm_type)
      }
      BIND(  URI(CONCAT(STR(?uri_cho),"#agg")) as ?uri_ore)
      OPTIONAL { ?uri_cho schema:creator ?creator }
      OPTIONAL { ?uri_cho schema:description ?description }
      OPTIONAL { ?uri_cho schema:genre ?genre . 
                FILTER (lang( ?genre) = "en")
                BIND(str(?genre) as ?dc_genre)
               }
      OPTIONAL { ?uri_cho schema:image ?image }
      OPTIONAL { ?uri_cho schema:keywords ?subject }
      OPTIONAL { ?uri_cho schema:about ?subject }
      OPTIONAL { ?uri_cho schema:license ?license }
      OPTIONAL { ?uri_cho schema:name ?name }
      OPTIONAL { 
         # use a default the provider mentioned in the dataset description, if any...
         ?dataset void:rootResource ?uri_cho .
         ?dataset schema:license ?license .
         ?dataset schema:provider ?provider . 
      }
      # use the provider name from the resource description
      OPTIONAL { ?uri_cho schema:provider ?provider }
      OPTIONAL { ?uri_cho schema:publisher ?publisher }
      OPTIONAL { ?uri_cho schema:sameAs ?sameAs }
      OPTIONAL { ?uri_cho schema:spatial ?spatial }
      OPTIONAL { ?uri_cho schema:temporal ?temporal }
      OPTIONAL { ?uri_cho schema:material ?material }
      OPTIONAL { ?uri_cho schema:url ?url }
      OPTIONAL { ?uri_cho schema:image ?isShownBy }
    }
    UNION 
    {
	    ?uri_org a schema:Organization .
        OPTIONAL { ?uri_org schema:name ?org_name }
        OPTIONAL { ?uri_org schema:description ?org_desc }
    }
    UNION
    {
	    ?uri_pers a schema:Person .
        OPTIONAL { ?uri_pers schema:name ?pers_name }
        OPTIONAL { ?uri_pers schema:additionalName ?pers_additionalName }
        OPTIONAL { ?uri_pers schema:birthDate ?pers_birthDate }
        OPTIONAL { ?uri_pers schema:deathDate ?pers_deathDate }
        OPTIONAL { ?uri_pers schema:description ?pers_desc }        
    }
    UNION
    {
	    ?uri_pl a schema:Place .
        OPTIONAL { ?uri_pl schema:name ?pl_name }
        OPTIONAL { ?uri_pl schema:alternateName ?pl_alternateName }
        OPTIONAL { ?uri_pl schema:sameAs ?pl_sameAs }        
    }
}